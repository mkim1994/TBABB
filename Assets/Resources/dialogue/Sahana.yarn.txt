title: 2OrderSahana
tags: 
colorID: 0
position: -888,-113
---
<<order Sahana 0 n 0 n 0 n 0>>
===
title: 2Sahana
tags: 
colorID: 0
position: -886,101
---
<<if $drinkScoreSahana is -1 and $stateSahana is -1>>
    <<set $hasOrderedSahana = false>>
    <<set $progressSahana = 0>>
    <<order Sahana 0 n 0 n 0 n 0>>
    <<set $stateSahana = 0>>
    Customer: Hello!
    Customer: Uhm...
    Customer: I've never been here before...
    [[First time drinking?|2SahanaA]]
    [[First time here?|2SahanaB]]
<<else>>
    <<if $progressSahana is 0>>
        <<if $drinkScoreSahana is -1>>
            <<set $hasOrderedSahana = true>>
            Customer: Anything, just not alcoholic...
        <<else>>
            <<if $hasOrderedSahana is false>>
                <<set $preemptiveSahana = true>>
                Customer: Uhm...wow!
                Customer: I hadn't even asked for anything yet...
            <<endif>>
            <<if $drinkScoreSahana is 0>>
                <<set $preemptiveSahana = false>>
                Customer: I think I just want something non-alcoholic.
                <<set $hasOrderedSahana = true>>
                Customer: That's still possible in a bar, right?
                Customer: ...Right?
            <<else>>
                <<if $drinkScoreSahana <= 10>>
                    Customer: Uhm...
                    Customer: I guess this amount is fine...?
                    Customer: ...No, actually, get me more please.
                <<else>>
                    <<set $stateSahana = 1>>
                    <<set $progressSahana = 1>>
                    Customer: Thank you!
                <<endif>>
            <<endif>>
        <<endif>>
    <<else>>
        <<if $progressSahana is 1>>
            <<set $progressSahana = 2>>
            <<if $drinkAlcoholSahana is 0>>
                <<if $preemptiveSahana is true>>
                    Customer: I don't know how you knew I wanted something like this.
                    Customer: I...don't drink, and was a little nervous coming here.
                    Customer: So this was a welcome surprise.
                <<else>>
                    Customer: This doesn't taste bitter at all.
                    Customer: Thank you.
                <<endif>>
                Customer: I know I'm kind of missing the point coming here to drink this but...
                Customer: It's just nice to get what I ask for, for once.
                Customer: I think that's all I needed right now.
                Customer: Thank you again--bye.
                <<set $stateSahana = 5>>
            <<else>>
                Customer: Mm.
                Customer: This is pretty bitter...
                Customer: I don't think I've tasted something like this before.
                <<if $preemptiveSahana is true>>
                    Customer: I...
                    Customer: I guess it's my fault for drinking this without even thinking...
                    Customer: I assume this is a weird question to ask at a bar but...
                    Customer: Is this alcoholic?
                <<else>>
                    Customer: You didn't actually give me something alcoholic, did you?
                <<endif>>
                
                [[Nope. No alcohol.|2SahanaA2]]
                [[No promises.|2SahanaB2]]
            <<endif>>
        <<else>>
        <<endif>>
    <<endif>>
<<endif>>
===
title: 2SahanaA
tags: 
colorID: 0
position: -653,-113
---
Customer: Haha, yes...
<<order Sahana 0 n 0 n 0 n 0>>
<<set $hasOrderedSahana = true>>
Customer: Uhm... give me anything, just no alcohol.
===
title: 2SahanaB
tags: 
colorID: 0
position: -520,109
---
Customer: Yes, but I mean I've never been to a bar in general.
Customer: Or drank anything alcoholic.
Customer: I might try it but...
<<order Sahana 0 n 0 n 0 n 0>>
<<set $hasOrderedSahana = true>>
Customer: Nevermind, give me anything, just no alcohol.
===
title: 2SahanaA2
tags: 
colorID: 0
position: -881,366
---
Customer: Alright.
Customer: I mean...
<<if $preemptiveOrder is true>>
    Customer: I should've just told you what I wanted.
    Customer: Ugh.
<<else>>
    Customer: What kind of a bartender would you be if you didn't do what I asked?
<<endif>>
Customer: Then again, no one listens to what I say, so whatever.
Customer: Fuck you and fuck all of them.
Customer: Thinking I'm a fucking idiot.
Customer: You're the idiot.
Customer: ...
Customer: I'm so sorry, I don't know what I just said.
Customer: I'm going to leave now, bye!
<<set $stateSahana = 5>>
===
title: 2SahanaB2
tags: 
colorID: 0
position: -1101,367
---
Customer: You're kidding.
Customer: ...
Customer: I guess it's more interesting this way.
Customer: Better than my day job, at least.
Customer: Fuck everyone there, honestly.
Customer: Thinking they can just step all over me.
Customer: Fuuuck.
Customer: ...
Customer: I'm so sorry, I don't know what I just said.
Customer: I'm. Going to leave now.
Customer: Bye.
<<set $stateSahana = 5>>
===
